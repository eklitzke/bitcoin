# Modififed version of AX_CHECK_COMPILE_FLAG that uses AC_LINK_IFELSE instead of
# AC_COMPILE_IFELSE.

AC_DEFUN([BITCOIN_CHECK_COMPILE_FLAG_AND_LINK],
[AC_PREREQ(2.64)dnl for _AC_LANG_PREFIX and AS_VAR_IF
AS_VAR_PUSHDEF([CACHEVAR],[ax_cv_check_[]_AC_LANG_ABBREV[]flags_and_link$4_$1])dnl
AC_CACHE_CHECK([whether _AC_LANG compiler accepts $1 and linking still works], CACHEVAR, [
  ax_check_save_flags=$[]_AC_LANG_PREFIX[]FLAGS
  _AC_LANG_PREFIX[]FLAGS="$[]_AC_LANG_PREFIX[]FLAGS $4 $1"
  AC_LINK_IFELSE([m4_default([$5],[AC_LANG_PROGRAM()])],
    [AS_VAR_SET(CACHEVAR,[yes])],
    [AS_VAR_SET(CACHEVAR,[no])])
  _AC_LANG_PREFIX[]FLAGS=$ax_check_save_flags])
AS_VAR_IF(CACHEVAR,yes,
  [m4_default([$2], :)],
  [m4_default([$3], :)])
AS_VAR_POPDEF([CACHEVAR])dnl
])dnl BITCOIN_CHECK_COMPILE_FLAG_AND_LINK
