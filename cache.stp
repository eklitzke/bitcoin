// global vars
global hits, misses, io, sys, vfs

function show_time_(event: string) {
  t = gettimeofday_ns()
  secs = t / 1000000000
  micros = t % 1000000000
  printf("t=%d.%09d event=%s", secs, micros, event)
}

function show_time(event: string) {
  show_time_(event)
  print(" ")
}

probe nd_syscall.open*, nd_syscall.close, nd_syscall.*map*, nd_syscall.brk, nd_syscall.stat {
  if (pid() == target()) {
    sys[name]++
  }
}

function rwhelper(name: string, devname: string, nbytes: long) {
  if (nbytes > 0 && pid() == target()) {
    if (devname == "N/A")
      tag = sprintf("%s.cached", name)
    else
      tag = sprintf("%s.uncached", name)
    io[tag] += nbytes
  }
}

// bitcoin does not use readv
probe vfs.read.return {
  rwhelper("read", devname, $return)
}

// bitcoin does not use writev
probe vfs.write.return {
  rwhelper("write", devname, $return)
}

probe vfs.* {
  if (pid() == target()) {
    vfs[name]++
  }
}

probe bitcoind = process("/home/evan/code/bitcoin/src/bitcoind") {
}

// record when cache flushes start
probe bitcoind.mark("cache__flush") {
  show_time("flush")
  printf("num_coins=%lu num_bytes=%lu\n", $arg1, $arg2)
}

// record cache hits
probe bitcoind.mark("cache__hit") {
  hits++
}

// record cache misses
probe bitcoind.mark("cache__miss") {
  misses++
}

probe bitcoind.mark("finish__ibd") {
  show_time("finish_ibd")
  println()
  exit()
}

/*
global dbgets
probe bitcoind.function("CCoinsViewDB::GetCoin") {
  dbgets++
}
*/

probe bitcoind.mark("update__tip") {
  show_time("update_tip")
  printf("height=%lu utxo_count=%lu utxo_memory=%lu ldb_count=%lu progress=0.%06d\n", $arg1, $arg2, $arg3, $arg4, $arg5)
}

probe timer.sec(1) {
  show_time_("syscalls")
  foreach ([name] in sys) {
    count = sys[name]
    printf(" %s=%d", name, count)
  }
  println()

  show_time("cache")
  printf("hits=%d misses=%d\n", hits, misses)
  hits=0
  misses=0

  show_time_("io")
  foreach([name] in io) {
    count = io[name]
    printf(" %s=%d", name, count)
  }
  println()

  show_time_("vfs")
  foreach([name] in vfs) {
    count = vfs[name]
    printf(" %s=%d", name, count)
  }
  println()

  delete sys
  delete io
  delete vfs
}
